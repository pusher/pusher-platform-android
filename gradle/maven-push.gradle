apply plugin: 'maven'
apply plugin: 'signing'

def properties = project.properties + System.properties

def isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
    return properties.get('RELEASE_REPOSITORY_URL', "https://oss.sonatype.org/service/local/staging/deploy/maven2/")
}

def getSnapshotRepositoryUrl() {
    return properties.get('SNAPSHOT_REPOSITORY_URL', "https://oss.sonatype.org/content/repositories/snapshots/")
}

def getRepositoryUsername() {
    return properties.get('NEXUS_USERNAME', "")
}

def getRepositoryPassword() {
    return properties.get('NEXUS_PASSWORD', "")
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task projectSourcesJar(type: Jar) {
        classifier = 'sources'
        if (project.plugins.findPlugin('com.android.library')) {
            from android.sourceSets.main.java.sourceFiles
        }
    }

    artifacts {
        archives projectSourcesJar
    }
}